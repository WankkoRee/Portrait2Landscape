plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp' version '1.7.22-1.0.8'
}

android {
    signingConfigs {
        release {
            storePassword ANDROID_STORE_PASSWORD
            keyPassword ANDROID_KEY_PASSWORD
            keyAlias ANDROID_KEY_ALIAS
            storeFile file(ANDROID_STORE_FILE)
            v1SigningEnabled true
            v2SigningEnabled true
            enableV3Signing true
        }
    }

    compileSdk 33

    defaultConfig {
        def git_count = "git rev-list HEAD --count".execute().text.trim()
        def git_hash = "git rev-list HEAD --abbrev-commit --max-count=1".execute().text.trim()

        applicationId "cn.wankkoree.xp.portrait2landscape"
        minSdk 21
        targetSdk 33
        versionCode 4
        versionName "v2.0.$git_count.$git_hash"
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources  true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = 11
    }

    namespace 'cn.wankkoree.xp.portrait2landscape'

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "${defaultConfig.applicationId}-${defaultConfig.versionName}_${defaultConfig.versionCode}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    def yukiHookApiVersion = '1.1.6'

    // Xposed Hook
    compileOnly 'de.robv.android.xposed:api:82'                                                     // Xposed
    implementation "com.highcapable.yukihookapi:api:$yukiHookApiVersion"                            // Yuki Hook API
    ksp "com.highcapable.yukihookapi:ksp-xposed:$yukiHookApiVersion"                                // Yuki Hook API - KSP

    // 语言特性支持
    implementation 'androidx.core:core-ktx:1.9.0'                                                   // Jetpack
}